// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Inc16(in=state, out=incremented);

    Or(a=reset, b=load, out=loadreset);
    Or(a=loadreset, b=inc,  out=loadresetinc);

    Mux16(a=incremented, b=in, sel=load, out=value);
    Mux16(a=value, b=false, sel=reset, out=final);

    Register(in=final, load=loadresetinc, out=state, out=out);
}
